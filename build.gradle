import org.gradle.internal.jvm.Jvm

plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

//noinspection GroovyAssignabilityCheck

private calcVersion() {
  hasProperty("teamcity") ? "1.0.${teamcity["build.number"]}" : ""
}

apply plugin: 'maven-publish'

apply from: 'project-libraries.gradle'

allprojects {
  version calcVersion()

  repositories {
    mavenCentral()
  }

  apply plugin: 'java'
  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  configurations {
    testArtifacts.extendsFrom testRuntime
  }

  dependencies {
    runtime files(Jvm.current().toolsJar)
  }

  sourceSets {
    main.java.srcDirs 'src'
  }
}

dependencies {
  compile project(':util')
}

//noinspection GrUnresolvedAccess
task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
}

task coverageAgentJar(type: Jar) {
  baseName = "coverage-agent"
  destinationDir = file("dist")
  manifest.attributes(
      "Premain-Class": "com.intellij.rt.coverage.main.CoveragePremain",
      "Boot-Class-Path": "$archiveName"
  )
  from(sourceSets.main.output)
  from(["util", "instrumentation"].collect({ project(":$it").sourceSets.main.output }))
  from { zipTree('lib/trove.jar') } {
    include(troveClasses())
  }
  from { zipTree('lib/asm-all.jar') } {
    include '**/*.class'
  }
}

// @formatter:off
private static troveClasses() {
  [  'THash'
   , 'Equality'
   , 'THashSet'
   , 'TIntHash'
   , 'TLongHash'
   , 'PrimeFinder'
   , 'TIntHashSet'
   , 'TObjectHash'
   , 'HashFunctions'
   , 'TPrimitiveHash'
   , 'TObjectFunction'
   , 'TIntObjectHashMap'
   , 'TObjectIntHashMap'
   , 'TLongObjectHashMap'
   , 'TIntHashingStrategy'
   , 'TIntObjectProcedure'
   , 'TLongObjectProcedure'
   , 'TLongHashingStrategy'
   , 'TObjectHashingStrategy'].collect { "org/jetbrains/coverage/**/${it}.class" }
}
// @formatter:on

build.dependsOn(coverageAgentJar)

clean.doFirst {
  delete "dist"
}

artifacts {
  archives coverageAgentJar
}

publishing {
  publications {
    CoveragePublication(MavenPublication) {
      groupId 'org.jetbrains.intellij.deps'
      artifactId 'intellij-coverage'
      version version
      artifact coverageAgentJar
    }
    TestDiscoveryPublication(MavenPublication) {
      groupId 'org.jetbrains.intellij.deps'
      artifactId 'intellij-test-discovery'
      version version
      artifact project(":testDiscovery").discoveryAgentJar
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')

  publish = true

  pkg {
    repo = 'intellij-third-party-dependencies'
    name = 'intellij-coverage'
    userOrg = 'jetbrains'

    version {
      name = version
    }
  }

  publications = ['CoveragePublication', 'TestDiscoveryPublication']
}