import org.gradle.internal.jvm.Jvm

//noinspection GroovyAssignabilityCheck
version "$version"

apply from: 'project-libraries.gradle'

allprojects {
  repositories {
    mavenCentral()
  }

  apply plugin: 'java'
  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  configurations {
    testArtifacts.extendsFrom testRuntime
  }

  dependencies {
    runtime files(Jvm.current().toolsJar)
  }

  sourceSets {
    main.java.srcDirs 'src'
  }
}

//noinspection GrUnresolvedAccess
task wrapper(type: Wrapper) {
  gradleVersion = '4.5'
}

task coveraAgentJar(type: Jar) {
  baseName = "coverage-agent"
  destinationDir = file("dist")
  manifest.attributes(
      "Premain-Class": "com.intellij.rt.coverage.main.CoveragePremain",
      "Boot-Class-Path": "$archiveName"
  )
  from(sourceSets.main.output)
  from(["util", "instrumentation"].collect({ project(":$it").sourceSets.main.output }))
  from { zipTree('lib/trove.jar') } {
    include '**/THash.class'
    include '**/Equality.class'
    include '**/THashSet.class'
    include '**/TIntHash.class'
    include '**/TLongHash.class'
    include '**/PrimeFinder.class'
    include '**/TIntHashSet.class'
    include '**/TObjectHash.class'
    include '**/HashFunctions.class'
    include '**/TPrimitiveHash.class'
    include '**/TObjectFunction.class'
    include '**/TIntObjectHashMap.class'
    include '**/TObjectIntHashMap.class'
    include '**/TLongObjectHashMap.class'
    include '**/TIntHashingStrategy.class'
    include '**/TIntObjectProcedure.class'
    include '**/TLongObjectProcedure.class'
    include '**/TLongHashingStrategy.class'
    include '**/TObjectHashingStrategy.class'
  }
  from { zipTree('lib/asm-all.jar') } {
    include '**/*.class'
  }
}

build.dependsOn(coveraAgentJar)