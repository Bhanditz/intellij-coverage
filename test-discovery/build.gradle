dependencies {
  compile library_project_asm_all
  compile project(':util')

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:2.9.0'
}

sourceSets {
  test.java.srcDirs = [file('test')]
}

task discoveryAgentJar(type: Jar) {
  baseName = test_discovery_jar_name
  destinationDir = file("../dist")
  manifest.attributes(
      "Premain-Class": "com.intellij.rt.coverage.testDiscovery.main.TestDiscoveryPremain",
      "Boot-Class-Path": "$archiveName"
  )
  from(sourceSets.main.output)
  from(["util"].collect({ project(":$it").sourceSets.main.output }))
  with repackLibs
}

build.dependsOn(discoveryAgentJar)

jar.enabled = false
artifacts {
  archives discoveryAgentJar
}